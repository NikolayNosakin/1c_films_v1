
Функция ТаблицаЗначенийВМассивСтруктур(ТаблицаЗначений) Экспорт
	
	Массив = Новый Массив();
	СтруктураСтрокой = "";
	НужнаЗапятая = Ложь;
	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		Если НужнаЗапятая Тогда
			СтруктураСтрокой = СтруктураСтрокой + ",";
		КонецЕсли;
		СтруктураСтрокой = СтруктураСтрокой + Колонка.Имя;
		НужнаЗапятая = Истина;
	КонецЦикла;
	Для Каждого Строка Из ТаблицаЗначений Цикл
		НоваяСтрока = Новый Структура(СтруктураСтрокой);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		Массив.Добавить(НоваяСтрока);
	КонецЦикла;
	Возврат Массив;
	
КонецФункции

//
// Параметры:
//    МассивСтруктур - МассивСтруктур - массив структур, которые будут преобразованы в таблиц значений.
//                                        Свойства структуры первого элемента массива определяют состав колонок результирующей таблицы.
//
// Возвращаемое значение:
//    ТаблицаЗначений - таблица значений, созданная из массива структур.
//
Функция МассивВТаблицуЗначений(МассивСтруктур) Экспорт
    Таблица = Новый ТаблицаЗначений;
    Если ТипЗнч(МассивСтруктур) = Тип("Массив") И МассивСтруктур.Количество() <> 0 Тогда
        ПерваяСтруктура = МассивСтруктур[0];
        Для Каждого Свойство Из ПерваяСтруктура Цикл
            Таблица.Колонки.Добавить(Свойство.Ключ, Новый ОписаниеТипов(Строка(ТипЗнч(Свойство.Значение))));
        КонецЦикла;
        
        Для Каждого Структура Из МассивСтруктур Цикл
            НоваяСтрока = Таблица.Добавить();
            ЗаполнитьЗначенияСвойств(НоваяСтрока, Структура);
        КонецЦикла;
    КонецЕсли;
    
    Возврат Таблица;
КонецФункции