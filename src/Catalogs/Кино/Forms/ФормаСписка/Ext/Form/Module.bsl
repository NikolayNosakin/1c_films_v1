
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Константы.СкрыватьПросмотренныеСерии.Получить() Тогда
		ФильтрПросмотр = 2;
		Элементы.НеПросмотренные.Пометка = Истина;
	Иначе
		ФильтрПросмотр = 0; 
		Элементы.БезФильтра.Пометка = Истина;
	КонецЕсли;	
	
	УстановитьПараметрДинамисческогоСписка(); 
	
	Пароль = Константы.ПарольДляВхода.Получить();
	Если НЕ Пароль = "" Тогда
		ВходПоПаролю = Истина;
	КонецЕсли;
	
	ВШС = Константы.ВысотаШрифтаСписка.Получить();
	Элементы.Список.Шрифт = Новый Шрифт(,?(ВШС = 0,8,ВШС));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ВходПоПаролю Тогда 
		ОткрытьФорму("ОбщаяФорма.ВходПоПаролю",,,,,,,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрДинамисческогоСписка()
	Список.Параметры.УстановитьЗначениеПараметра("ФильтрПросмотр", ФильтрПросмотр); 
	Список.Параметры.УстановитьЗначениеПараметра("Тип", Тип);
КонецПроцедуры

&НаКлиенте
Процедура ТипОтборПриИзменении(Элемент)
	УстановитьПараметрДинамисческогоСписка();
КонецПроцедуры

&НаКлиенте
Процедура Просмотренные(Команда)
	ФильтрПросмотр = 1;
	УстановитьПараметрДинамисческогоСписка();
	Элементы.Просмотренные.Пометка = Истина;
	Элементы.НеПросмотренные.Пометка = Ложь;
	Элементы.БезФильтра.Пометка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура НеПросмотренные(Команда)
	ФильтрПросмотр = 2;
	УстановитьПараметрДинамисческогоСписка();
	Элементы.Просмотренные.Пометка = Ложь;
	Элементы.НеПросмотренные.Пометка = Истина;
	Элементы.БезФильтра.Пометка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура БезФильтра(Команда)
	ФильтрПросмотр = 0;
	УстановитьПараметрДинамисческогоСписка();
	Элементы.Просмотренные.Пометка = Ложь;
	Элементы.НеПросмотренные.Пометка = Ложь;
	Элементы.БезФильтра.Пометка = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЖанрыНажатие(Элемент)
	ОткрытьФорму("Справочник.Жанры.ФормаСписка");
КонецПроцедуры

&НаКлиенте
Процедура СтраныНажатие(Элемент)
	ОткрытьФорму("Справочник.Страны.ФормаСписка");
КонецПроцедуры

&НаКлиенте
Процедура НастройкиНажатие(Элемент)
	ОткрытьФорму("ОбщаяФорма.Настройки");
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	ТС = Элементы.Список.ТекущаяСтрока;
	Если НЕ ТС = Неопределено Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередУдалениемЗавершение", ЭтотОбъект,ТС), Нстр("ru = 'Удалить ""'; en = 'Delete ""'") + Строка(ТС) + """?", РежимДиалогаВопрос.ДаНет,,,Нстр("ru = 'Удаление'; en = 'Removal'"));	 
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередУдалениемЗавершение(РезультатВопроса, Ссылка) Экспорт	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда		
		УдалитьСервер(Ссылка);
		Элементы.Список.Обновить();
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура УдалитьСервер(Ссылка) 
	ОБ = Ссылка.ПолучитьОбъект();
	ОБ.Удалить();	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ИзменениеВысотыШрифтаСписка" Тогда
		Элементы.Список.Шрифт = Новый Шрифт(,Параметр);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(,Значение);
КонецПроцедуры
